#* Settings
set unstable

#* Imports
import 'scripts/common.just'

#* Modules
#? âœ¨ Project-specific
mod proj 'scripts/proj.just'
#? ğŸŒ Install
mod inst 'scripts/inst.just'

#* Shells
set shell :=\
  ['pwsh', '-NonInteractive', '-NoProfile', '-CommandWithArgs']
set script-interpreter :=\
  ['pwsh', '-NonInteractive', '-NoProfile']

#* Python packages
dev :=\
  uvr + sp + quote(env("PROJECT_NAME") + '-dev')
pipeline :=\
  uvr + sp + quote(env("PROJECT_NAME") + '-pipeline')

#* â™¾ï¸ Self

# ğŸ“ƒ [DEFAULT] List recipes
[group('â™¾ï¸  Self')]
list:
  {{j}} --list

[group('â™¾ï¸  Self')]
just *args:
  {{j}} {{args}}

#* â›°ï¸ Environments

# ğŸƒ Run shell commands with uv synced...
[group('â›°ï¸ Environments')]
run *args: uv-sync
  @{{ if args==empty { quote(YELLOW+'No command given'+NORMAL) } else {empty} }}
  -{{ if args!=empty { j + ';' + sp + args } else {empty} }}

# ğŸ‘¥ Run recipes as a contributor...
[group('â›°ï¸ Environments')]
con *args: uv-sync
  {{j}} _sync_settings_json _sync_env_yml{{ if env("PRE_COMMIT", empty)=='1' { \
    sp + 'con-git-submodules' \
  } else {empty} }}{{ if env("VSCODE_FOLDER_OPEN_TASK_RUNNING", empty)=='1' { \
    sp + 'con-git-submodules' + sp + 'con-pre-commit-hooks' \
  } else {empty} }}
  {{ if args!=empty { j + sp + args } else {empty} }}

# ğŸ¤– Run recipes in CI...
[group('â›°ï¸ Environments')]
ci *args: uv-sync
  {{j}} _sync-ci-path-file _sync-ci-env-file; \
  {{pre}} Set-Content {{pyright_config}} ({{j}} {{dev}} elevate-pyright-warnings)
  {{ if args!=empty { ';' + sp + j + sp + args } else {empty} }}
  if (!$Env:CI) { {{pre}} Remove-Item {{pyright_config}} }

pyright_config :=\
  'pyrightconfig.json'

# Add `.venv` tools to CI path. Needed for some GitHub Actions like pyright
[script, group('â›°ï¸ Environments')]
_sync-ci-path-file:
  {{script_pre}}
  $DevCiPathFile = {{quote(env("DEV_CI_PATH_FILE"))}}
  if (!(Test-Path $DevCiPathFile)) { New-Item $DevCiPathFile | Out-Null }
  if ( !(Get-Content $DevCiPathFile | Select-String -Pattern '.venv') ) {
    $Workdir = $PWD -replace '\\', '/'
    Add-Content $DevCiPathFile ("$Workdir/.venv/bin", "$Workdir/.venv/scripts")
  }

# Write environment vars to CI environment file
[script, group('â›°ï¸ Environments')]
_sync-ci-env-file:
  {{script_pre}}
  $DevCiEnvFile = {{quote(env("DEV_CI_ENV_FILE"))}}
  $CiEnv = Merge-Envs (('base', 'ci') | Get-Env) | Format-Env -Upper
  $CiEnvText = ''
  $CiEnv['CI_ENV_SET'] = '1'
  $CiEnv.GetEnumerator() | ForEach-Object { $CiEnvText += "$($_.Name)=$($_.Value)`n" }
  if (!(Test-Path $DevCiEnvFile)) { New-Item $DevCiEnvFile | Out-Null }
  if (!(Get-Content $DevCiEnvFile | Select-String -Pattern 'CI_ENV_SET')) {
      $CiEnvText | Add-Content -NoNewline $DevCiEnvFile
  }

# ğŸ‘¥ Write environment vars to temporary `.env`-like environment file
[script, group('â›°ï¸ Environments')]
sync-contrib-env-file:
  {{script_pre}}
  $DevEnvFile = New-TemporaryFile
  $Environ = Merge-Envs (('base', 'contrib') | Get-Env) | Format-Env -Upper
  $EnvironText = ''
  $Environ.GetEnumerator() | ForEach-Object { $EnvironText += "$($_.Name)=$($_.Value)`n" }
  $EnvironText | Set-Content -NoNewline $DevEnvFile
  "$DevEnvFile"

# ğŸ“¦ Run recipes in devcontainer
[script, group('â›°ï¸ Environments')]
@devcontainer *args:
  {{script_pre}}
  {{'#?'+BLUE+sp+'Devcontainers need submodules explicitly marked as safe directories'+NORMAL}}
  $Repo = Get-ChildItem '/workspaces'
  $Packages = Get-ChildItem "$Repo/packages"
  $SafeDirs = @($Repo) + $Packages
  foreach ($Dir in $SafeDirs) {
    if (!($SafeDirs -contains $Dir)) { git config --global --add safe.directory $Dir }
  }
  {{ if args==empty { 'return' } else { '#?'+BLUE+sp+'Run recipe'+NORMAL } }}
  {{ if args==empty {empty} else { j + sp + args } }}

# Sync environment variables to '.vscode/settings.json'
[script, group('â›°ï¸ Environments')]
_sync_settings_json:
  {{script_pre}}
  $Environ = (Merge-Envs (('base', 'contrib') | Get-Env)) | Format-Env -Upper
  $JsonEnviron = $Environ | ConvertTo-Json -Compress
  $Settings = '.vscode/settings.json'
  $SettingsContent = Get-Content $Settings -Raw
  $AnyChanged = $false
  foreach ($Plat in ('linux', 'osx', 'windows')) {
    $Pat = '(?m)"terminal\.integrated\.env\.' + $Plat + '"\s*:\s*(?<SettingsEnv>\{[^}]*\})'
    $Repl = '"terminal.integrated.env.' + $Plat + '": ' + $JsonEnviron
    if (!($SettingsContent -Match $Pat)) { continue }
    if (
      ($Matches['SettingsEnv'] | ConvertFrom-Json | ConvertTo-Json -Compress) -ne
      $JsonEnviron
    ) {
      $AnyChanged = $true
      $SettingsContent = $SettingsContent -replace $Pat, $Repl
    }
  }
  if ($AnyChanged) {
    Set-Content $Settings $SettingsContent -NoNewline
    try { {{uvr}} pre-commit run 'prettier' --files $Settings | Out-Null } catch {}
  }

# Sync environment variables to '.github/workflows/env.yml'
[script, group('â›°ï¸ Environments')]
_sync_env_yml:
  {{script_pre}}
  $Environ = Merge-Envs (('answers', 'base') | Get-Env)
  $LimitedEnviron = [ordered]@{}
  (Limit-Env $Environ '{{ci_variables}}'.Split()).GetEnumerator() |
    ForEach-Object { $LimitedEnviron[$_.Name] = @{ value = $_.Value } }
  $LimitedEnviron = $LimitedEnviron | Format-Env -Lower
  $Workflow = '.github/workflows/env.yml'
  $WorkflowData = Get-Content $Workflow | ConvertFrom-Yaml -Ordered
  if (
    ($WorkflowData.on.workflow_call.outputs | ConvertTo-Json -Compress) -ne
    ($LimitedEnviron | ConvertTo-Json -Compress)
  ) {
    Set-Content $Workflow @'
  # Environment variables
  #! Please only update by modifying `env.json` then running `./j.ps1 con` to sync
  '@
    $WorkflowData.on.workflow_call.outputs = $LimitedEnviron
    $WorkflowData | ConvertTo-Yaml | Add-Content $Workflow -NoNewline
    try { {{uvr}} pre-commit run 'trailing-whitespace' --files $Workflow | Out-Null } catch {}
    try { {{uvr}} pre-commit run 'mixed-line-ending' --files $Workflow | Out-Null } catch {}
  }

ci_variables :=\
  'actions_runner' \
  + sp + 'project_name' \
  + sp + 'project_version' \
  + sp + 'publish_project' \
  + sp + 'pyright_python_pylance_version' \
  + sp + 'uv_version'

#* ğŸŸ£ uv

#? uv invocations
uv_options :=\
  '--all-packages' + sp + '--python' + sp + quote(python_version)
uvr :=\
  uv + sp + 'run' + sp + uv_options
uvs :=\
  uv + sp + 'sync' + sp + uv_options

# ğŸŸ£ uv ...
[group('ğŸŸ£ uv')]
uv *args:
  {{pre}} {{uv}} {{args}}

# ğŸƒ uv run ...
[group('ğŸŸ£ uv')]
uv-run *args:
  {{pre}} {{uvr}} {{args}}

# ğŸƒ uvx ...
[group('ğŸŸ£ uv')]
uvx *args:
  {{pre}} {{uv}} {{args}}

# ğŸ”ƒ uv sync ...
[group('ğŸŸ£ uv')]
uv-sync *args:
  {{pre}} {{uvs}} {{args}}

#* ğŸ Python

# ğŸ python ...
[group('ğŸ Python')]
py *args:
  {{pre}} {{uvr}} 'python' {{args}}

# ğŸ“¦ uv run --module ...
[group('ğŸ Python')]
py-module module *args:
  {{pre}} {{uvr}} '--module' {{quote(module)}} {{args}}

# ğŸƒ uv run python -c '...'
[group('ğŸ Python')]
py-command cmd:
  {{pre}} {{uvr}} 'python' '-c' {{quote(cmd)}}

# ğŸ“„ uv run --script ...
[group('ğŸ Python')]
py-script script *args:
  {{pre}} {{uvr}} '--script' {{quote(script)}} {{args}}

# ğŸ“º uv run --gui-script ...
[windows, group('ğŸ Python')]
py-gui script *args:
  {{pre}} {{uvr}} '--gui-script' {{quote(script)}} {{args}}

# âŒ uv run --gui-script ...
[linux, macos, group('âŒ Python (N/A for this OS)')]
py-gui:
  @{{quote(GREEN+'GUI scripts'+sp+na+NORMAL)}}

#* âš™ï¸ Tools

# ğŸ§ª pytest ...
[group('âš™ï¸  Tools')]
tool-pytest *args:
  {{pre}} {{uvr}} pytest {{args}}

# ğŸ“– preview docs
[group('âš™ï¸  Tools')]
tool-docs-preview:
  {{pre}} {{uvr}} sphinx-autobuild --show-traceback docs _site \
    {{ prepend( '--ignore', "'**/temp' '**/data' '**/apidocs' '**/*schema.json'" ) }}

# ğŸ“– build docs
[group('âš™ï¸  Tools')]
tool-docs-build:
  {{pre}} {{uvr}} sphinx-build -EaT 'docs' '_site'

# ğŸ”µ pre-commit run ...
[group('âš™ï¸  Tools')]
tool-pre-commit *args:
  {{pre}} {{uvr}} pre-commit run --verbose {{args}}

# ğŸ”µ pre-commit run --all-files ...
[group('âš™ï¸  Tools')]
tool-pre-commit-all *args:
  {{j}} tool-pre-commit --all-files {{args}}

# âœ”ï¸  Check that the working tree is clean
[group('âš™ï¸  Tools')]
tool-check-clean:
  {{pre}} if (git status --porcelain) { \
    throw 'Files changed when syncing contributor environment. Please commit and push changes with `./j.ps1 con`.' \
  }

# âœ”ï¸  fawltydeps ...
[group('âš™ï¸  Tools')]
tool-fawltydeps *args:
  {{pre}} {{uvr}} fawltydeps {{args}}

# âœ”ï¸  pyright
[group('âš™ï¸  Tools')]
tool-pyright:
  {{pre}} {{uvr}} pyright

# âœ”ï¸  ruff check ... '.'
[group('âš™ï¸  Tools')]
tool-ruff *args:
  {{pre}} {{uvr}} ruff check {{args}} .

#* ğŸ“¦ Packaging

# ğŸ›  Build wheel, compile binary, and sign...
[group('ğŸ“¦ Packaging')]
pkg-build *args:
  {{pre}} {{uvr}} {{env("PROJECT_NAME")}} {{args}}

# ğŸ“œ Build changelog for new version
[group('ğŸ“¦ Packaging')]
pkg-build-changelog version:
  {{pre}} {{templ-sync}} --data 'env("PROJECT_VERSION")={{version}}'
  {{pre}} {{uvr}} towncrier build --yes --version '{{version}}'
  {{pre}} {{post_template_task}}
  -{{pre}} try { git stage 'changelog/*.md' } catch {}
  @{{quote(YELLOW+'Changelog draft built. Please finalize it, then run `./j.ps1 pkg-release`.'+NORMAL)}}

# âœ¨ Release the current version
[group('ğŸ“¦ Packaging')]
pkg-release:
  {{pre}} git add --all
  {{pre}} git commit -m '{{env("PROJECT_VERSION")}}'
  {{pre}} git tag --force --sign -m {{env("PROJECT_VERSION")}} {{env("PROJECT_VERSION")}}
  {{pre}} git push

#* ğŸ‘¥ Contributor environment setup

# ğŸ‘¥ Update Git submodules
[group('ğŸ‘¥ Contributor environment setup')]
con-git-submodules:
  {{pre}} Get-ChildItem '.git/modules' -Filter 'config.lock' -Recurse -Depth 1 | \
      Remove-Item
  {{pre}} git submodule update --init --merge

# ğŸ‘¥ Install pre-commit hooks
[group('ğŸ‘¥ Contributor environment setup')]
con-pre-commit-hooks:
  {{pre}} if ( \
    ({{quote(hooks)}} -Split {{quote(sp)}} | \
      ForEach-Object { ".git/hooks/$_" } | \
      Test-Path \
    ) -Contains $False \
  ) { \
    {{uvr}} pre-commit install --install-hooks | Out-Null; \
    Write-Host -ForegroundColor 'Green' 'Pre-commit hooks installed.' \
  }
hooks :=\
  'pre-commit'

# ğŸ‘¥ Normalize line endings
[group('ğŸ‘¥ Contributor environment setup')]
con-norm-line-endings:
  -{{pre}} try { {{uvr}} pre-commit run 'mixed-line-ending' --all-files | Out-Null } catch {}

# ğŸ‘¥ Run dev task...
[group('ğŸ‘¥ Contributor environment setup')]
con-dev *args:
  {{pre}} {{dev}} {{args}}

# ğŸ‘¥ Run pipeline stage...
[group('ğŸ‘¥ Contributor environment setup')]
con-pipeline *args:
  {{pre}} {{pipeline}} {{args}}

# ğŸ‘¥ Update changelog...
[group('ğŸ‘¥ Contributor environment setup')]
con-update-changelog change_type:
 {{pre}} {{dev}} add-change {{change_type}}

# ğŸ‘¥ Update changelog with the latest commit's message
[group('ğŸ‘¥ Contributor environment setup')]
con-update-changelog-latest-commit:
  {{pre}} {{uvr}} towncrier create \
    "+$((Get-Date).ToUniversalTime().ToString('o').Replace(':','-')).change.md" \
    --content ( \
      "$(git log -1 --format='%s') ([$(git rev-parse --short HEAD)]" \
      + '(' \
        + 'https://github.com/{{env("PROJECT_OWNER_GITHUB_USERNAME")}}/{{env("GITHUB_REPO_NAME")}}' \
        + "/commit/$(git rev-parse HEAD))" \
      + ')' \
      + "`n" \
    )

#* ğŸ“¤ CI Output

# ğŸ·ï¸  Set CI output to latest release
[group('ğŸ“¤ CI Output')]
ci-out-latest-release:
  {{pre}} Set-Content {{env("DEV_CI_OUTPUT_FILE")}} "latest_release=$( \
    ($Latest = gh release list --limit 1 --json tagName | \
      ConvertFrom-Json | Select-Object -ExpandProperty 'tagName' \
    ) ? $Latest : '-1' \
  )"

#* ğŸ§© Templating

# â¬†ï¸  Update from template
[group('ğŸ§© Templating')]
templ-update:
  {{pre}} {{update_template}} --defaults
  {{pre}} {{post_template_task}}

# â¬†ï¸  Update from template (prompt)
[group('ğŸ§© Templating')]
templ-update-prompt:
  {{pre}} {{update_template}}
  {{pre}} {{post_template_task}}

# ğŸ”ƒ Sync with current template
[group('ğŸ§© Templating')]
templ-sync:
  {{pre}} {{templ-sync}}
  {{pre}} {{post_template_task}}
templ-sync :=\
  sync_template + sp + '--defaults'

# ğŸ”ƒ Sync with current template (prompt)
[group('ğŸ§© Templating')]
templ-sync-prompt:
  {{pre}} {{sync_template}}
  {{pre}} {{post_template_task}}

# â¡ï¸  Recopy current template
[group('ğŸ§© Templating')]
templ-recopy:
  {{pre}} {{recopy_template}} --defaults
  {{pre}} {{post_template_task}}

# â¡ï¸  Recopy current template (prompt)
[group('ğŸ§© Templating')]
templ-recopy-prompt:
  {{pre}} {{recopy_template}}
  {{pre}} {{post_template_task}}

update_template :=\
  copier_update + sp + latest_template
sync_template :=\
  copier_update + sp + current_template
recopy_template :=\
  copier_recopy + sp + current_template
post_template_task :=\
  'git add --all; git reset;' + sp + j + sp + 'con'
latest_template :=\
  quote('--vcs-ref=HEAD')
current_template :=\
  quote('--vcs-ref=' + env("TEMPLATE_COMMIT"))
copier_recopy :=\
  copier + sp + 'recopy'
copier_update :=\
  copier + sp + 'update'
copier :=\
  uvx + sp + quote('copier@' + env("COPIER_VERSION"))

#* ğŸ› ï¸ Repository setup

# ğŸ¥¾ Initialize repository
[group('ğŸ› ï¸ Repository setup')]
repo-init:
  {{j}} _repo-init-set-up-remote
  {{j}} _repo-set-up-push

# Initialize repo and set up remote if repo is fresh
[script, group('ğŸ› ï¸ Repository setup')]
_repo-init-set-up-remote:
  {{script_pre}}
  git init
  try { git rev-parse HEAD } catch {
    gh repo create --public --source '.'
    (Get-Content -Raw '.copier-answers.yml') -Match '(?m)^project_description:\s(.+\n(?:\s{4}.+)*)'
    if ($Matches) {
    }
    gh repo edit --description ($Matches[1] -Replace "`n", ' ' -Replace ' {4}', '')
    $Matches = $null
    gh repo edit --homepage 'https://{{env("PROJECT_OWNER_GITHUB_USERNAME")}}.github.io/{{env("GITHUB_REPO_NAME")}}/'
  }

# Set up repo and push
[script, group('ğŸ› ï¸ Repository setup')]
_repo-set-up-push:
  {{script_pre}}
  git submodule add --force --name 'typings' 'https://github.com/softboiler/python-type-stubs.git' 'typings'
  git submodule add --force --name 'context-models' 'https://github.com/softboiler/context-models.git' 'packages/context-models'
  git submodule add --force --name 'cachier' 'https://github.com/blakeNaccarato/cachier.git' 'packages/cachier'
  Set-Location 'packages/cachier'
  git checkout 'db7f394a553a90944ca25c2d687771006e77a024'
  Set-Location '../..'
  git add --all
  {{j}} con
  git add --all
  try { git commit --no-verify -m 'Prepare template using softboiler/copier-pipeline' }
  catch {}
  git push

#* ğŸ’» Machine setup

# ğŸ‘¤ Set Git username and email
[group('ğŸ’» Machine setup')]
setup-git username email:
  {{pre}} git config --global user.name {{quote(username)}}
  {{pre}} git config --global user.email {{quote(email)}}

# ğŸ‘¤ Configure Git as recommended
[group('ğŸ’» Machine setup')]
setup-git-recs:
  {{pre}} git config --global fetch.prune true
  {{pre}} git config --global pull.rebase true
  {{pre}} git config --global push.autoSetupRemote true
  {{pre}} git config --global push.followTags true

# ğŸ”‘ Log in to GitHub API
[group('ğŸ’» Machine setup')]
setup-gh:
  {{pre}} gh auth login

# ğŸ”“ Allow running local PowerShell scripts
[windows, group('ğŸ’» Machine setup')]
setup-scripts:
  {{pre}} Set-ExecutionPolicy -Scope 'CurrentUser' 'RemoteSigned'
# âŒ Allow running local PowerShell scripts
[linux, macos, group('âŒ Machine setup (N/A for this OS)')]
setup-scripts:
  @{{quote(GREEN+'Allowing local PowerShell scripts to run'+sp+na+NORMAL)}}
